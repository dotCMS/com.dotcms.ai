plugins {
    id("biz.aQute.bnd.builder") version "6.4.0"
    id 'maven-publish'
    id("net.researchgate.release") version "3.0.2"
}

group = 'com.dotcms'
version = '1.0-SNAPSHOT'

configurations {
    osgiLibs
}
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url "https://repo.dotcms.com/artifactory/libs-release" }

}


dependencies {
    implementation 'com.knuddels:jtokkit:0.6.1'
    implementation 'com.dotcms:dotcms:23.10.24'

    //implementation fileTree(include: ["*.jar"], dir: "/Users/will/git/dotcms/core/dotcms-war/target/dotcms-war-1.0.0-SNAPSHOT/WEB-INF/lib")


    implementation (group: 'com.pgvector', name: 'pgvector', version: '0.1.3')
    implementation 'io.netty:netty-all:4.1.63.Final'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'


    osgiLibs 'com.knuddels:jtokkit:0.6.1'
    osgiLibs (group: 'com.pgvector', name: 'pgvector', version: '0.1.3') {
        exclude group: 'org.postgresql', module: 'postgresql'
    }
    osgiLibs 'io.netty:netty-all:4.1.63.Final'


    //osgiLibs 'com.github.jtidy:jtidy:1.0.5'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = "$System.env.ARTIFACTORY_USER"
                password = "$System.env.ARTIFACTORY_PASSWD"
            }

            if(project.version.endsWith('-SNAPSHOT')) {
                println 'PUBLISHING dotAI SNAPSHOT'
                url "https://repo.dotcms.com/artifactory/libs-snapshot-local"
            } else {
                println 'PUBLISHING dotAI RELEASE'
                url "https://repo.dotcms.com/artifactory/libs-release-local"
            }
        }
    }
}

test {
    useJUnitPlatform()
}

task cleanLibFiles(type: Delete) {
    delete fileTree("src/main/resources/libs").matching {
        include "**/*"
    }
}

task copyToLib(type: Copy) {
    from configurations.osgiLibs
    into "src/main/resources/libs"

}

clean.dependsOn(cleanLibFiles)

processResources.dependsOn(copyToLib)

jar {
    manifest {
        attributes(
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - AI, Embeddings and Vector Search',
                'Bundle-DocURL': 'https://www.dotcms.com/',
                'Bundle-Activator': 'com.dotcms.ai.Activator',
                'Import-Package': '*;version=0',
                'Bundle-ClassPath' : '.;libs/jtokkit-0.6.1.jar;libs/pgvector-0.1.3.jar;libs/netty-all-4.1.63.Final.jar',
                'Require-Capability' : ""
        )
    }


    bundle {
        bnd(
            "-noee": "true",
            "-sources": "false",
            "-contract": "!JavaServlet,*"
        )
    }

}
