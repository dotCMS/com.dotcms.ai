plugins {
    id("biz.aQute.bnd.builder") version "6.4.0"
}

group = 'com.dotcms'
version = '1.0-SNAPSHOT'

configurations {
    osgiLibs
}
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url "https://repo.dotcms.com/artifactory/libs-release" }

}

dependencies {
    implementation 'com.knuddels:jtokkit:0.6.1'
    implementation 'com.dotcms:dotcms:23.06'
    //implementation fileTree(include: ["*.jar"], dir: "/Users/will/git/dotcms/core/dotcms-war/target/dotcms-war-1.0.0-SNAPSHOT/WEB-INF/lib")

    implementation 'com.pgvector:pgvector:0.1.3'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    osgiLibs 'com.knuddels:jtokkit:0.6.1'
    osgiLibs 'com.pgvector:pgvector:0.1.3'
    //osgiLibs 'com.github.jtidy:jtidy:1.0.5'
}

test {
    useJUnitPlatform()
}

task cleanLibFiles(type: Delete) {
    delete fileTree("src/main/resources/libs").matching {
        include "**/*"
    }
}

task copyToLib(type: Copy) {
    from configurations.osgiLibs
    into "src/main/resources/libs"

}

clean.dependsOn(cleanLibFiles)


processResources.dependsOn(copyToLib)
/////////////////////////
//Plugin jar
/////////////////////////
jar {
    manifest {
        attributes(
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - AI, Embeddings and Vector Search',
                'Bundle-DocURL': 'https://www.dotcms.com/',
                'Bundle-Activator': 'com.dotcms.ai.Activator',
                'Import-Package': '*;version=0',
                'Bundle-ClassPath' : '.;libs/jtokkit-0.6.1.jar;libs/pgvector-0.1.3.jar',
                'Require-Capability' : ""
        )
    }


    bundle {
        bnd(
            "-noee": "true",
            "-sources": "false",
            "-contract": "!JavaServlet,*"
        )
    }

}
